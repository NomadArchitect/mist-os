# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/perf/test.gni")

if (is_fuchsia) {
  rustc_binary("power-framework-benchmarks") {
    name = "power_framework_microbenchmarks"
    with_unit_tests = false
    testonly = true
    edition = "2018"
    sources = [
      "src/main.rs",
      "src/work.rs",
    ]
    deps = [
      "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
      "//src/developer/fuchsia-criterion",
      "//src/lib/fuchsia-component",
      "//src/lib/zircon/rust:fuchsia-zircon",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:criterion",
    ]

    configs -= [ "//build/config/rust/lints:allow_unused_results" ]
    configs += [ ":benchmark" ]
  }

  fuchsia_package_with_single_component("power_framework_microbenchmarks") {
    deps = [
      ":power-framework-benchmarks",
      "//src/power/broker:component",
      "//src/power/system-activity-governor:component",
    ]
    testonly = true
    manifest = "meta/power-framework-microbenchmarks.cml"
  }
}

rustc_test("bin") {
  name = "integration"
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:criterion",
  ]

  sources = [
    "src/lib.rs",
    "src/work.rs",
  ]
}

fuchsia_component("component") {
  testonly = true
  component_name = "integration"
  manifest = "meta/integration.cml"
  deps = [ ":bin" ]
}

config("benchmark") {
  rustflags = [ "--cfg=benchmark" ]
}

# Run with `fx test power-framework-bench-integration-tests`.
fuchsia_test_package("package") {
  package_name = "power-framework-bench-integration-tests"
  test_components = [ ":component" ]
  deps = [
    "//src/power/broker:component",
    "//src/power/system-activity-governor:component",
  ]
}

group("integration") {
  testonly = true
  deps = [ ":package" ]
}

if (is_host) {
  fuchsia_component_perf_test("power_framework_microbenchmarks") {
    package = ":power_framework_microbenchmarks"
    component_name = "power_framework_microbenchmarks"
    expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.power.sag.txt"
  }
}

group("bench") {
  testonly = true
  deps = [ ":power_framework_microbenchmarks($host_toolchain)" ]
}
