# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

_boards = [
  "astro",
  "luis",
  "moonflower",
  "nelson",
  "sherlock",
  "uefi",
  "vim3",
]

foreach(board_name, _boards) {
  executable("paver-${board_name}") {
    defines = [ "${board_name}" ]
    sources = [ "paver.cc" ]
    deps = [
      "//sdk/fidl/fuchsia.paver:fuchsia.paver_cpp",
      "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_cpp",
      "//sdk/lib/async-loop:async-loop-cpp",
      "//sdk/lib/async-loop:async-loop-default",
      "//sdk/lib/component/outgoing/cpp",
      "//sdk/lib/fdio",
      "//src/storage/lib/paver:${board_name}",
      "//src/sys/lib/stdout-to-debuglog/cpp",
    ]
  }

  fuchsia_component("component-${board_name}") {
    component_name = "paver"
    manifest = "meta/paver-${board_name}.cml"
    deps = [ ":paver-${board_name}" ]
  }

  fuchsia_package("package-${board_name}") {
    package_name = "paver"
    deps = [ ":component-${board_name}" ]
  }
}

# Legacy targets to be deleted.
executable("paver") {
  defines = [ "LEGACY_PAVER" ]
  sources = [ "paver.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fdio",
    "//src/storage/lib/paver",
    "//src/sys/lib/stdout-to-debuglog/cpp",
  ]
}

fuchsia_component("component") {
  component_name = "paver"
  manifest = "meta/paver-legacy.cml"
  deps = [ ":paver" ]
}

fuchsia_package("package") {
  package_name = "paver"
  deps = [ ":component" ]
}
