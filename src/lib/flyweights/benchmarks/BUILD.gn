# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/perf/test.gni")

rustc_binary("flyweights_benches_bin") {
  testonly = true
  name = "flyweights_benches"
  edition = "2021"
  sources = [ "src/main.rs" ]
  deps = [
    "//src/developer/fuchsia-criterion",
    "//src/lib/flyweights",
    "//third_party/rust_crates:ahash",
  ]
}

fuchsia_component("flyweights_benches_comp") {
  testonly = true
  component_name = "flyweights_benches"
  manifest = "meta/benches.cml"
  deps = [ ":flyweights_benches_bin" ]
}

fuchsia_package("flyweights_benches") {
  testonly = true
  deps = [ ":flyweights_benches_comp" ]
}

fuchsia_component_perf_test("flyweights_benches_runner") {
  package = ":flyweights_benches"
  component_name = "flyweights_benches"
  expected_metric_names_filepath =
      "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.flyweights.txt"
}

group("benchmarks") {
  testonly = true
  deps = [ ":flyweights_benches_runner" ]
}
