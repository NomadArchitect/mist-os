# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

rustc_library("fidl_next") {
  edition = "2021"
  sources = [
    "src/chunk.rs",
    "src/decode/error.rs",
    "src/decode/mod.rs",
    "src/decoder/basic.rs",
    "src/decoder/mod.rs",
    "src/encode/error.rs",
    "src/encode/mod.rs",
    "src/encoder/basic.rs",
    "src/encoder/mod.rs",
    "src/lib.rs",
    "src/owned.rs",
    "src/slot.rs",
    "src/take.rs",
    "src/test_util.rs",
    "src/wire/boxed.rs",
    "src/wire/envelope.rs",
    "src/wire/error.rs",
    "src/wire/mod.rs",
    "src/wire/ptr.rs",
    "src/wire/string/mod.rs",
    "src/wire/string/optional.rs",
    "src/wire/string/required.rs",
    "src/wire/table.rs",
    "src/wire/union.rs",
    "src/wire/vec/mod.rs",
    "src/wire/vec/optional.rs",
    "src/wire/vec/raw.rs",
    "src/wire/vec/required.rs",
  ]
  deps = [
    "//third_party/rust_crates:munge",
    "//third_party/rust_crates:rend",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:zerocopy",
  ]

  if (is_fuchsia) {
    sources += [
      "src/fuchsia/decoder.rs",
      "src/fuchsia/encoder.rs",
      "src/fuchsia/mod.rs",
      "src/fuchsia/wire/handle.rs",
      "src/fuchsia/wire/mod.rs",
    ]
    deps += [ "//sdk/rust/zx" ]
  }

  with_unit_tests = true

  # NOTE: this library is still experimental
  visibility = [
    ":*",
    "//:developer_universe_packages",
    "//tools/fidl/fidlc/testdata/*",
  ]
}

fuchsia_unittest_package("fidl_next_test_package") {
  deps = [ ":fidl_next_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":fidl_next_test($host_toolchain)",
    ":fidl_next_test_package",
  ]
}
