# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD_style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":metadata_server_test" ]
}

group("drivers") {
  testonly = true
  deps = [
    "metadata-forwarder-test-driver:component",
    "metadata-retriever-test-driver:component",
    "metadata-sender-test-driver:component",
  ]
}

bind_library("metadata_server_test_bind_library") {
  testonly = true
  source = "metadata.server.test.bind"
  public_deps = [ "//src/devices/bind/fuchsia.test" ]
}

test("metadata-server-test") {
  testonly = true
  output_name = "metadata_server_test"
  sources = [ "metadata_server_test.cc" ]

  deps = [
    "fuchsia.hardware.test:fuchsia.hardware.test_cpp",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_cpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/driver_test_realm/realm_builder/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_unittest_package("metadata_server_test") {
  test_specs = {
    # Allow error logging for when `MetadataServer::GetMetadata()` fails.
    log_settings = {
      max_severity = "ERROR"
    }
  }

  deps = [
    ":metadata-server-test",
    "metadata-forwarder-test-driver:component",
    "metadata-retriever-test-driver:component",
    "metadata-sender-test-driver:component",
  ]
}
