# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/export_fuchsia_package_to_bazel.gni")
import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("element_launcher_bin") {
  output_name = "element_launcher"
  with_unit_tests = true
  edition = "2021"

  deps = [
    ":element_launcher_config",
    "//sdk/fidl/fuchsia.element:fuchsia.element_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:log",
  ]
  test_deps = [ "//third_party/rust_crates:futures" ]

  sources = [ "src/main.rs" ]
}

fuchsia_component_manifest("element_launcher_component_manifest") {
  manifest = "meta/element_launcher.cml"
  component_name = "element_launcher"
}

fuchsia_structured_config_rust_lib("element_launcher_config") {
  cm_label = ":element_launcher_component_manifest"
}

fuchsia_component("element_launcher_component") {
  cm_label = ":element_launcher_component_manifest"
  deps = [ ":element_launcher_bin" ]
}

fuchsia_structured_config_values("element_launcher_empty_config") {
  cm_label = ":element_launcher_component_manifest"
  values_source = "//src/ui/bin/element_launcher/meta/empty_config.json5"
}

fuchsia_package("element_launcher") {
  package_name = "element_launcher"

  deps = [
    ":element_launcher_component",
    ":element_launcher_component_manifest",
    ":element_launcher_empty_config",
  ]
}

# Make `element_launcher` available to products assembled with Bazel.
export_fuchsia_package_to_bazel("element_launcher_bazel_inputs") {
  package = ":element_launcher"
}

fuchsia_unittest_package("element_launcher_tests") {
  deps = [ ":element_launcher_bin_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":element_launcher_tests" ]
}
