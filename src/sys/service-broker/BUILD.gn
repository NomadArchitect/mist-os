# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("service-broker") {
  testonly = true
  deps = [
    ":package",
    ":tests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":service-broker-unittests" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "service-broker"
  manifest = "meta/service_broker.cml"
}

fuchsia_structured_config_rust_lib("service_broker_config") {
  cm_label = ":manifest"
}

rustc_binary("bin") {
  output_name = "service-broker"
  edition = "2021"

  configs += [ "//build/config/rust:bootfs" ]

  with_unit_tests = true

  deps = [
    ":service_broker_config",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/rust/zx",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  package_name = "service-broker"
  deps = [ ":component" ]
}

fuchsia_unittest_package("service-broker-unittests") {
  deps = [ ":bin_test" ]
}
