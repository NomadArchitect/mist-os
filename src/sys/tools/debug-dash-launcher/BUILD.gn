# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

fuchsia_component_manifest("manifest") {
  component_name = "debug-dash-launcher"
  manifest = "meta/debug_dash_launcher.cml"
}

fuchsia_structured_config_rust_lib("debug_dash_launcher_config") {
  cm_label = ":manifest"
}

rustc_binary("bin") {
  output_name = "debug-dash-launcher"
  edition = "2021"
  with_unit_tests = true

  configs += [ "//build/config/rust:bootfs" ]

  deps = [
    ":debug_dash_launcher_config",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_rust",
    "//sdk/fidl/fuchsia.dash:fuchsia.dash_rust",
    "//sdk/fidl/fuchsia.device:fuchsia.device_rust",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_rust",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//sdk/fidl/fuchsia.process:fuchsia.process_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//sdk/rust/zx",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/storage/lib/vfs/rust:vfs",
    "//src/sys/lib/moniker",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:indexmap",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:url",
  ]

  test_deps = [
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:tempfile",
  ]

  # The dash shell is only used in eng products, not production.
  deps += [ "//build/validate:non_production_tag" ]

  sources = [
    "src/launch.rs",
    "src/launch/component.rs",
    "src/launch/package.rs",
    "src/layout.rs",
    "src/main.rs",
    "src/package_resolver.rs",
    "src/socket.rs",
    "src/trampoline.rs",
  ]
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_unittest_package("debug-dash-launcher-unittests") {
  manifest = "meta/debug_dash_launcher_unittests.cml"
  deps = [
    ":bin_test",
    "//src/bringup/bin/ptysvc:component",
  ]
  test_type = "system"
}

fuchsia_package("debug-dash-launcher") {
  deps = [
    ":component",
    "//third_party/sbase:bins",
    "//zircon/third_party/uapp/dash",
  ]
  # This package is added by product assembly into non-eng builds only.
  # It should not be added manually anywhere else.
  #
  # This is temporarily relaxed for a transition
  # visibility = [ "//bundles/assembly/*" ]
}

group("tests") {
  testonly = true
  deps = [
    ":debug-dash-launcher-unittests",
    "integration_test",
  ]
}
