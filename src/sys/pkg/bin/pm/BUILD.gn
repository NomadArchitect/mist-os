# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/go/go_binary.gni")
import("//build/host.gni")

if (is_host) {
  test_packages = [
    "build",
    "pkg",
    "pmhttp",
    "repo",
  ]

  test_labels = []
  foreach(pkg, test_packages) {
    pkg_ = string_replace(pkg, "/", "_")
    name = "pm_${pkg_}_test"
    test_labels += [ "$pkg:$name" ]
  }

  group("host_tests") {
    testonly = true
    public_deps = []
    foreach(label, test_labels) {
      public_deps += [ label ]
    }
  }

  go_binary("pm_bin") {
    output_name = "pm"
    library = "cmd/pm:main"
    sdk_category = "partner"
  }
}

install_host_tools("host") {
  deps = [ ":pm_bin" ]
  outputs = [ "pm" ]
}
