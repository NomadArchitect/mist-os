# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_component_with_expectations.gni")

group("tests") {
  testonly = true
  deps = [ ":enumeration-integration-test" ]
}

executable("example_test") {
  testonly = true
  sources = [ "example_test_main.cc" ]
}

executable("check_path_test") {
  testonly = true
  sources = [ "check_path_test_main.cc" ]
}

fuchsia_test_component("example-test-component") {
  component_name = "example-test"
  manifest = "meta/example-test.cml"
  deps = [ ":example_test" ]
}

fuchsia_test_component("check-path-test-component") {
  component_name = "check-path-test"
  manifest = "meta/check-path-test.cml"
  deps = [ ":check_path_test" ]
}

resource("enumeration-integration-test-config") {
  sources = [ "enumeration_integration_test_config.json5" ]
  outputs = [ "enumeration_test_adapter_config/config/test_config.json5" ]
}

resource("empty-file") {
  sources = [ "empty" ]
  outputs = [ "test_data_dir/empty" ]
}

fuchsia_test_component_with_expectations(
    "enumeration-integration-test-component") {
  expectations = "enumeration-integration-test-expectations.json5"
  manifest = "meta/enumeration-integration-test.cml"
  component_name = "enumeration-integration-test"
}

fuchsia_package("enumeration-integration-test-package") {
  testonly = true
  subpackages =
      [ "//src/tee/testing/enumeration_test_adapter:enumeration-test-adapter" ]
  deps = [
    ":check-path-test-component",
    ":empty-file",
    ":enumeration-integration-test-component",
    ":enumeration-integration-test-config",
    ":example-test-component",
  ]
}

fuchsia_test("enumeration-integration-test") {
  package = ":enumeration-integration-test-package"
  component = ":enumeration-integration-test-component"
}
