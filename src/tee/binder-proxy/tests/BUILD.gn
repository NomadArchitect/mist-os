# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/components/fuchsia_test.gni")
import("//build/components/fuchsia_test_component.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/rust/rustc_binary.gni")
import("//src/tee/manager/ta_manager_config.gni")

group("tests") {
  testonly = true
  deps = [
    ":binder-proxy-enumerate-empty-tests",
    ":binder-proxy-enumerate-two-tas-tests",
    ":binder_proxy_host_tool($host_toolchain)",
  ]
}

rustc_binary("binder_proxy_enumerate_tests") {
  edition = "2021"
  source_root = "src/binder_proxy_enumerate.rs"
  sources = [ source_root ]
  deps = [
    ":binder_proxy_tests_config",
    "//sdk/rust/zx",
    "//src/lib/android/binder/rust:binder",
    "//src/lib/android/binder/rust/rpcbinder",
    "//src/lib/fuchsia",
    "//src/paravirtualization/lib/vsock/vsock_sys",
    "//src/tee/binder-proxy:android_system_microfuchsia_vm_service",
    "//src/tee/lib/dev_urandom_compat",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:log",
  ]
}

template("binder_proxy_test") {
  _base_name = target_name
  test_root_component_target = _base_name + "-root"
  ta_manager_config_target = _base_name + "-ta-manager-config"
  test_package_target = _base_name

  fuchsia_test_component(test_root_component_target) {
    component_name = _base_name + "-root"
    manifest = "meta/${component_name}.cml"
  }

  ta_manager_config(ta_manager_config_target) {
    config_sources = invoker.ta_manager_config_sources
  }

  fuchsia_test_package(test_package_target) {
    test_components = [ ":${test_root_component_target}" ]
    deps = [ ":component" ]
    subpackages = [
      "//src/paravirtualization/testing:vsock-for-test",
      "//src/tee/binder-proxy:binder-proxy",
      "//src/tee/manager:ta-manager",
      ":${ta_manager_config_target}",
    ]
  }
}

fuchsia_component_manifest("manifest") {
  component_name = "binder-proxy-enumerate-tests"
  manifest = "meta/binder-proxy-enumerate-tests.cml"
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":binder_proxy_enumerate_tests" ]
}

fuchsia_structured_config_rust_lib("binder_proxy_tests_config") {
  cm_label = ":manifest"
}

binder_proxy_test("binder-proxy-enumerate-empty-tests") {
  ta_manager_config_sources = [
    # Intentionally empty.
  ]
}

binder_proxy_test("binder-proxy-enumerate-two-tas-tests") {
  ta_manager_config_sources = [
    "//src/tee/manager/tests/config/185d0391-bb47-495a-ba57-d6c6b808bfae.json",  # noop
    "//src/tee/manager/tests/config/7672c06d-f8b3-482b-b8e2-f88fcc8604d7.json",  # panic
  ]
}

if (is_host) {
  executable("binder_proxy_host_tool") {
    sources = [ "binder_proxy_host_tool.cc" ]
    deps = [
      "//sdk/lib/syslog/cpp",
      "//src/lib/android/binder",
      "//src/lib/fxl:cli",
      "//src/tee/binder-proxy:android_system_microfuchsia_trusted_app_cpp",
      "//src/tee/binder-proxy:android_system_microfuchsia_vm_service_cpp",
    ]
  }
}
