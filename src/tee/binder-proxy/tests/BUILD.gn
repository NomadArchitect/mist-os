# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test.gni")
import("//build/components/fuchsia_test_component.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/rust/rustc_test.gni")

group("tests") {
  testonly = true
  deps = [
    ":binder-proxy-tests",
    ":binder_proxy_host_tool($host_toolchain)",
  ]
}

rustc_test("binder_proxy_tests") {
  edition = "2021"
  source_root = "src/main.rs"
  sources = [ source_root ]
  deps = [
    "//sdk/rust/zx",
    "//src/lib/android/binder/rust:binder",
    "//src/lib/android/binder/rust/rpcbinder",
    "//src/lib/fuchsia",
    "//src/paravirtualization/lib/vsock/vsock_sys",
    "//src/tee/binder-proxy:android_system_microfuchsia_vm_service",
    "//src/tee/lib/dev_urandom_compat",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_test_component("binder-proxy-tests-component") {
  component_name = "binder-proxy-tests"
  manifest = "meta/binder-proxy-tests.cml"
  deps = [ ":binder_proxy_tests" ]
}

fuchsia_test_component("binder-proxy-tests-root") {
  component_name = "binder-proxy-tests-root"
  manifest = "meta/binder-proxy-tests-root.cml"
}

fuchsia_test_package("binder-proxy-tests") {
  test_components = [ ":binder-proxy-tests-root" ]
  deps = [ ":binder-proxy-tests-component" ]
  subpackages = [
    "//src/paravirtualization/testing:vsock-for-test",
    "//src/tee/binder-proxy",
  ]
}

if (is_host) {
  executable("binder_proxy_host_tool") {
    sources = [ "binder_proxy_host_tool.cc" ]
    deps = [
      "//sdk/lib/syslog/cpp",
      "//src/lib/android/binder",
      "//src/lib/fxl:cli",
      "//src/tee/binder-proxy:android_system_microfuchsia_vm_service_cpp",
    ]
  }
}
