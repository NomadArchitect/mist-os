# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test.gni")
import("//build/components/fuchsia_test_component.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/rust/rustc_test.gni")
import("//src/tee/manager/tests/ta_manager_config_test.gni")

ta_manager_config_test("ta-manager-empty-config-test") {
  config_sources = []
  test_source_root = "src/empty_config.rs"
}

ta_manager_config_test("ta-manager-noop-ta-test") {
  config_sources = [ "config/185d0391-bb47-495a-ba57-d6c6b808bfae.json" ]
  test_source_root = "src/noop_ta.rs"
  deps = [ "//sdk/fidl/fuchsia.tee:fuchsia.tee_rust" ]
  subpackages = [ "//src/tee/ta/noop:ta-noop" ]
}

ta_manager_config_test("ta-manager-connect-panic-ta-test") {
  config_sources = [ "config/7672c06d-f8b3-482b-b8e2-f88fcc8604d7.json" ]
  test_source_root = "src/connect_panic_ta.rs"
  deps = [
    "//sdk/fidl/fuchsia.tee:fuchsia.tee_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/tee/tee_internal_api:tee_internal",
  ]
  subpackages = [ "//src/tee/ta/panic:ta-panic" ]
}

ta_manager_config_test("ta-manager-enumerate-config-test") {
  config_sources = [
    "config/185d0391-bb47-495a-ba57-d6c6b808bfae.json",
    "config/7672c06d-f8b3-482b-b8e2-f88fcc8604d7.json",
  ]
  test_source_root = "src/enumerate.rs"
}

ta_manager_config_test("ta-manager-ta-read-props-test") {
  config_sources = [ "config/932517f3-e807-48f8-b584-fbeec848c2b6.json" ]
  test_source_root = "src/ta_read_props.rs"
  deps = [
    "//sdk/fidl/fuchsia.tee:fuchsia.tee_rust",
    "//src/tee/tee_internal_api:tee_internal",
  ]
  subpackages = [ "//src/tee/ta/read_props:ta-read-props" ]
}

rustc_test("test_props") {
  edition = "2021"

  source_root = "src/properties.rs"
  sources = [ "src/properties.rs" ]
  deps = [
    "//src/lib/fuchsia",
    "//src/tee/lib/tee_properties",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_test_component("test-props-component") {
  component_name = "test-props"
  manifest = "meta/test-props.cml"
  deps = [ ":test_props" ]
}

fuchsia_test_component("test-props-root") {
  manifest = "meta/ta-manager-properties-test-root.cml"
}

fuchsia_package("test-props-package") {
  testonly = true
  deps = [
    ":test-props-component",
    ":test-props-root",
  ]
  subpackages = [ "//src/tee/manager:ta-manager-realm-test" ]
}

fuchsia_test("ta-manager-props-test") {
  package = ":test-props-package"
  component = ":test-props-root"
}

group("tests") {
  testonly = true
  deps = [
    ":ta-manager-connect-panic-ta-test",
    ":ta-manager-empty-config-test",
    ":ta-manager-enumerate-config-test",
    ":ta-manager-noop-ta-test",
    ":ta-manager-props-test",
    ":ta-manager-ta-read-props-test",
  ]
}
