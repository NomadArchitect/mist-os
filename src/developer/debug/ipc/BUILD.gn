# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This library is shared between the client and agent code.

import("//build/cpp/sdk_source_set.gni")

# Deprecated mechanism used for host tools compatibility versioning.
# This will be replaced with proper API-level based compatibility mechanisms.
import("//build/config/fuchsia/platform_version.gni")
config("fuchsia_api_level_for_developer_tools") {
  visibility = [ ":*" ]
  defines = [
    # TODO(https://fxbug.dev/308808613): Eliminate the need for this in order
    # to unblock https://fxbug.dev/305961460.
    # TODO(https://fxbug.dev/326277078): Add 1 when switching to NEXT.
    "NEXT_STABLE_API_LEVEL=${platform_version.deprecated_highest_numbered_api_level}",
  ]
}

sdk_source_set("ipc") {
  category = "internal"
  build_as_static = true

  sources = [
    "automation_instruction.cc",
    "automation_instruction.h",
    "decode_exception.cc",
    "decode_exception.h",
    "filter_utils.cc",
    "filter_utils.h",
    "message_reader.cc",
    "message_reader.h",
    "message_writer.cc",
    "message_writer.h",
    "protocol.cc",
    "protocol.h",
    "records.cc",
    "records.h",
    "unwinder_support.cc",
    "unwinder_support.h",
  ]

  deps = [
    "//src/developer/debug/shared",
    "//zircon/system/public",
  ]

  public_deps = [
    "//src/lib/fxl",
    "//src/lib/unwinder",
  ]

  public_configs = [ ":fuchsia_api_level_for_developer_tools" ]
}

# Unit tests for this directory. These are intended to be referenced by unit
# test targets for the consumers of this library.
source_set("tests") {
  testonly = true
  sources = [
    "automation_instruction_unittests.cc",
    "decode_exception_unittests.cc",
    "filter_utils_unittests.cc",
    "message_unittests.cc",
    "protocol_unittests.cc",
  ]

  deps = [
    "//src/developer/debug/shared",
    "//src/developer/debug/shared:test_support",
    "//third_party/googletest:gtest",
  ]

  public_deps = [ ":ipc" ]
}
