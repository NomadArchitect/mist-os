# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_staticlib.gni")

block_server_sources = [
  "src/async_interface.rs",
  "src/lib.rs",
]

block_server_deps = [
  "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_rust",
  "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_rust",
  "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_rust",
  "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_rust",
  "//src/lib/fuchsia-async",
  "//src/lib/zircon/rust:fuchsia-zircon",
  "//src/storage/lib/block_protocol",
  "//third_party/rust_crates:anyhow",
  "//third_party/rust_crates:futures",
]

rustc_library("block_server") {
  with_unit_tests = true
  edition = "2021"
  sources = block_server_sources
  deps = block_server_deps
  test_deps = [
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
  ]
}

fuchsia_unittest_package("block_server_tests") {
  deps = [ ":block_server_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":block_server_tests" ]
}
