# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":install-disk-image-test-package" ]
}

group("disk-pave") {
  deps = [ ":install-disk-image" ]
}

source_set("payload-streamer") {
  public = [ "payload-streamer.h" ]
  sources = [ "payload-streamer.cc" ]
  visibility = [ ":*" ]
  deps = [ "//sdk/lib/async-default" ]
  public_deps = [
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
  ]
}

executable("install-disk-image") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "disk-pave.cc" ]
  deps = [
    ":payload-streamer",
    "//sdk/fidl/fuchsia.fshost:fuchsia.fshost_cpp",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_cpp",
    "//sdk/lib/async",
    "//sdk/lib/async-default",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fdio",
    "//src/storage/lib/paver",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
  ]
}

bootfs_files_for_assembly("bootfs") {
  deps = [ ":install-disk-image" ]
}

test("install-disk-image-test") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "install-disk-image-test"
  sources = [ "test/payload-streamer-test.cc" ]
  deps = [
    ":payload-streamer",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_cpp",
    "//sdk/lib/async",
    "//sdk/lib/async-default",
    "//sdk/lib/async-loop",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("install-disk-image-test-package") {
  deps = [ ":install-disk-image-test" ]
  manifest = "meta/install-disk-image-test.cml"
}
