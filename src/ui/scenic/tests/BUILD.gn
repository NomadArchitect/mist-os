# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/lib/vulkan/vulkan.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":display-integration-tests",
    ":flatland-cpu-renderer-integration-test",
    ":flatland-integration-tests",
    ":flatland-null-renderer-integration-test",
    ":flatland-pixeltest-yuv",
    ":flatland-protected-memory-integration-test",
    ":observer-integration-tests",
  ]
}

# TODO(https://fxbug.dev/42175597): use common-deps-zxtest for CTF tests.
source_set("common-deps-gtest") {
  testonly = true
  sources = [
    "utils/blocking_present.cc",
    "utils/blocking_present.h",
    "utils/utils.cc",
    "utils/utils.h",
  ]
  public_deps = [
    ":scenic-realm-builder",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/testing/util:loop_fixture",
    "//src/ui/testing/util:screenshot_helper",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
  deps = [ "//src/lib/fostr/fidl/fuchsia.ui.input:fuchsia.ui.input" ]
}

source_set("common-deps-zxtest") {
  testonly = true
  sources = [
    "utils/blocking_present.cc",
    "utils/blocking_present.h",
    "utils/utils.cc",
    "utils/utils.h",
  ]
  public_deps = [
    ":scenic-realm-builder",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/ui/testing/util:loop_fixture",
    "//src/ui/testing/util:screenshot_helper",
    "//zircon/system/ulib/zxtest",
  ]
  deps = [ "//src/lib/fostr/fidl/fuchsia.ui.input:fuchsia.ui.input" ]
}

# TODO(https://fxbug.dev/42175597): use common-deps-zxtest for CTF tests.
source_set("common-deps-zxtest-with-realm-builder") {
  testonly = true
  public_deps = [
    ":common-deps-zxtest",
    ":scenic-realm-builder",
  ]
}

source_set("scenic-realm-builder") {
  testonly = true
  sources = [
    "utils/scenic_realm_builder.cc",
    "utils/scenic_realm_builder.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_hlcpp",
    "//sdk/fidl/fuchsia.media:fuchsia.media_hlcpp",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_hlcpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_hlcpp",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//sdk/lib/syslog/cpp",
    "//src/ui/testing/ui_test_realm:test_realms",
  ]
}

source_set("screen-capture-utils") {
  testonly = true
  sources = [
    "utils/screen_capture_utils.cc",
    "utils/screen_capture_utils.h",
  ]
  public_deps = [
    ":common-deps-gtest",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/flatland/engine",
    "//zircon/system/ulib/fbl",
  ]
}

executable("flatland-allocation-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/flatland_allocation_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/utils:utils",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("flatland-allocation-test") {
  component_name = "flatland-allocation-test"
  manifest = "meta/flatland_allocation_test.cml"
  deps = [ ":flatland-allocation-test-bin" ]
  test_type = "vulkan"
}

executable("flatland-touch-integration-test-bin") {
  testonly = true
  sources = [
    "flatland_integration_tests/flatland_touch_integration_test.cc",
    "flatland_integration_tests/flatland_touch_local_hit_integration_test.cc",
  ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointer:fuchsia.ui.pointer_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointer.augment:fuchsia.ui.pointer.augment_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointerinjector:fuchsia.ui.pointerinjector_hlcpp",
  ]
}

fuchsia_test_component("flatland-touch-integration-test") {
  component_name = "flatland-touch-integration-test"
  manifest = "meta/flatland_touch_integration_test.cml"
  deps = [ ":flatland-touch-integration-test-bin" ]
  test_type = "vulkan"
}

executable("flatland-focus-integration-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/flatland_focus_integration_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.focus:fuchsia.ui.focus_hlcpp",
  ]
}

fuchsia_test_component("flatland-focus-integration-test") {
  component_name = "flatland-focus-integration-test"
  manifest = "meta/flatland_focus_integration_test.cml"
  deps = [ ":flatland-focus-integration-test-bin" ]
  test_type = "vulkan"
}

executable("flatland-view-ref-installed-integration-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/flatland_view_ref_installed_integration_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.views:fuchsia.ui.views_hlcpp",
  ]
}

fuchsia_test_component("flatland-view-ref-installed-integration-test") {
  component_name = "flatland-view-ref-installed-integration-test"
  manifest = "meta/flatland_view_ref_installed_integration_test.cml"
  deps = [ ":flatland-view-ref-installed-integration-test-bin" ]
  test_type = "vulkan"
}

executable("flatland-view-integration-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/flatland_view_integration_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.display.singleton:fuchsia.ui.display.singleton_hlcpp",
    "//sdk/fidl/fuchsia.ui.views:fuchsia.ui.views_hlcpp",
  ]
}

fuchsia_test_component("flatland-view-integration-test") {
  component_name = "flatland-view-integration-test"
  manifest = "meta/flatland_view_integration_test.cml"
  deps = [ ":flatland-view-integration-test-bin" ]
  test_type = "vulkan"
}

executable("cpu-renderer-integration-test-bin") {
  testonly = true

  # TODO(https://fxbug.dev/42075218): Re-enable this test once zx_cache_flush is implemented on
  # riscv64.
  if (target_cpu != "riscv64") {
    sources = [ "flatland_integration_tests/cpu_renderer_integration_test.cc" ]
  }
  deps = [
    ":common-deps-gtest",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/flatland/buffers",
    "//src/ui/scenic/lib/utils:utils",
  ]
}

fuchsia_test_component("cpu-renderer-integration-test") {
  component_name = "cpu-renderer-integration-test"
  manifest = "meta/cpu_renderer_integration_test.cml"
  deps = [ ":cpu-renderer-integration-test-bin" ]
  test_type = "system"
}

executable("null-renderer-integration-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/null_renderer_integration_test.cc" ]
  deps = [
    ":common-deps-gtest",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/utils:utils",
  ]
}

fuchsia_test_component("null-renderer-integration-test") {
  component_name = "null-renderer-integration-test"
  manifest = "meta/null_renderer_integration_test.cml"
  deps = [ ":null-renderer-integration-test-bin" ]
  test_type = "system"
}

executable("observer-registry-integration-test-bin") {
  testonly = true
  sources =
      [ "observer_integration_tests/observer_registry_integration_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.focus:fuchsia.ui.focus_hlcpp",
    "//sdk/fidl/fuchsia.ui.observation.geometry:fuchsia.ui.observation.geometry_hlcpp",
    "//sdk/fidl/fuchsia.ui.observation.test:fuchsia.ui.observation.test_hlcpp",
  ]
}

fuchsia_test_component("observer-registry-integration-test") {
  component_name = "observer-registry-integration-test"
  manifest = "meta/observer_registry_integration_test.cml"
  deps = [ ":observer-registry-integration-test-bin" ]
  test_type = "vulkan"
}

executable("scoped-observer-registry-integration-test-bin") {
  testonly = true
  sources = [
    "observer_integration_tests/scoped_observer_registry_integration_test.cc",
  ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.focus:fuchsia.ui.focus_hlcpp",
    "//sdk/fidl/fuchsia.ui.observation.geometry:fuchsia.ui.observation.geometry_hlcpp",
    "//sdk/fidl/fuchsia.ui.observation.scope:fuchsia.ui.observation.scope_hlcpp",
  ]
}

fuchsia_test_component("scoped-observer-registry-integration-test") {
  component_name = "scoped-observer-registry-integration-test"
  manifest = "meta/scoped_observer_registry_integration_test.cml"
  deps = [ ":scoped-observer-registry-integration-test-bin" ]
  test_type = "vulkan"
}

executable("protected-memory-integration-test-bin") {
  testonly = true
  sources =
      [ "flatland_integration_tests/protected_memory_integration_test.cc" ]
  deps = [
    ":common-deps-gtest",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/utils:utils",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("protected-memory-integration-test") {
  component_name = "protected-memory-integration-test"
  manifest = "meta/protected_memory_integration_test.cml"
  deps = [ ":protected-memory-integration-test-bin" ]
  test_type = "vulkan"
}

executable("screen-capture-integration-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/screen_capture_integration_test.cc" ]
  deps = [
    ":common-deps-gtest",
    ":screen-capture-utils",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/screen_capture:screen_capture",
    "//src/ui/scenic/lib/utils:utils",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("screen-capture-integration-test") {
  component_name = "screen-capture-integration-test"
  manifest = "meta/screen_capture_integration_test.cml"
  deps = [ ":screen-capture-integration-test-bin" ]
  test_type = "vulkan"
}

executable("screen-capture2-integration-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/screen_capture2_integration_test.cc" ]
  deps = [
    ":common-deps-gtest",
    ":screen-capture-utils",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition.internal:fuchsia.ui.composition.internal_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//src/ui/lib/escher",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/flatland/engine",
    "//src/ui/scenic/lib/screen_capture2:screen_capture2",
    "//src/ui/scenic/lib/utils:utils",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("screen-capture2-integration-test") {
  component_name = "screen-capture2-integration-test"
  manifest = "meta/screen_capture2_integration_test.cml"
  deps = [ ":screen-capture2-integration-test-bin" ]
  test_type = "vulkan"
}

executable("display-ownership-integration-test-bin") {
  testonly = true
  sources = [ "display_ownership_integration_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    ":scenic-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition.internal:fuchsia.ui.composition.internal_hlcpp",
    "//src/ui/scenic/lib/utils:utils",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("display-ownership-integration-test") {
  component_name = "display-ownership-integration-test"
  manifest = "meta/display_ownership_integration_test.cml"
  deps = [ ":display-ownership-integration-test-bin" ]
  test_type = "vulkan"
}

executable("singleton-display-integration-test-bin") {
  testonly = true
  sources = [ "singleton_display_integration_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    ":scenic-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.display.singleton:fuchsia.ui.display.singleton_hlcpp",
    "//src/ui/scenic/lib/utils:utils",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("singleton-display-integration-test") {
  component_name = "singleton-display-integration-test"
  manifest = "meta/singleton_display_integration_test.cml"
  deps = [ ":singleton-display-integration-test-bin" ]
  test_type = "vulkan"
}

executable("flatland-mouse-integration-test-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/flatland_mouse_integration_test.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointer:fuchsia.ui.pointer_hlcpp",
    "//sdk/fidl/fuchsia.ui.pointerinjector:fuchsia.ui.pointerinjector_hlcpp",
    "//src/ui/scenic/lib/utils",
  ]
}

fuchsia_test_component("flatland-mouse-integration-test") {
  component_name = "flatland-mouse-integration-test"
  manifest = "meta/flatland_mouse_integration_test.cml"
  deps = [ ":flatland-mouse-integration-test-bin" ]
  test_type = "vulkan"
}

executable("flatland-pixeltest-bin") {
  testonly = true
  sources = [ "flatland_integration_tests/flatland_pixeltest.cc" ]
  deps = [
    ":common-deps-zxtest-with-realm-builder",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.compression.internal:fuchsia.ui.compression.internal_cpp",
    "//sdk/fidl/fuchsia.ui.display.singleton:fuchsia.ui.display.singleton_hlcpp",
    "//src/lib/testing/loop_fixture",
    "//src/ui/scenic/lib/allocation:allocation",
    "//src/ui/scenic/lib/utils",
    "//src/ui/scenic/lib/utils:utils",
    "//src/ui/testing/util:screenshot_helper",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("flatland-pixeltest-flip-and-orientation") {
  component_name = "flatland-pixeltest-flip-and-orientation"
  manifest = "meta/flatland_pixeltest_flip_and_orientation.cml"
  deps = [ ":flatland-pixeltest-bin" ]
  test_type = "vulkan"
}

fuchsia_test_component("flatland-pixeltest-general") {
  component_name = "flatland-pixeltest-general"
  manifest = "meta/flatland_pixeltest_general.cml"
  deps = [ ":flatland-pixeltest-bin" ]
  test_type = "vulkan"
}

fuchsia_test_package("flatland-integration-tests") {
  test_components = [
    ":flatland-allocation-test",
    ":flatland-focus-integration-test",
    ":flatland-mouse-integration-test",
    ":flatland-pixeltest-flip-and-orientation",
    ":flatland-pixeltest-general",
    ":flatland-touch-integration-test",
    ":flatland-view-ref-installed-integration-test",
    ":flatland-view-integration-test",
    ":screen-capture-integration-test",
    ":screen-capture2-integration-test",
  ]
  test_specs = {
    environments = vulkan_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_test_component("pixeltest-yuv") {
  component_name = "flatland-pixeltest-yuv"
  manifest = "meta/flatland_pixeltest_yuv.cml"
  deps = [ ":flatland-pixeltest-bin" ]
  test_type = "vulkan"
}

fuchsia_test_package("flatland-pixeltest-yuv") {
  test_components = [ ":pixeltest-yuv" ]
  test_specs = {
    # TODO(https://fxbug.dev/42137913): Skip this test for AEMU as YUV Sysmem images are not supported yet.
    environments = magma_libvulkan_hardware_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_test_package("flatland-cpu-renderer-integration-test") {
  test_components = [ ":cpu-renderer-integration-test" ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_test_package("flatland-null-renderer-integration-test") {
  test_components = [ ":null-renderer-integration-test" ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_test_package("flatland-protected-memory-integration-test") {
  test_components = [ ":protected-memory-integration-test" ]

  test_specs = {
    environments = [
      astro_env,
      sherlock_env,
      nelson_env,
    ]
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_test_package("observer-integration-tests") {
  test_components = [
    ":observer-registry-integration-test",
    ":scoped-observer-registry-integration-test",
  ]
  test_specs = {
    environments = vulkan_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_test_package("display-integration-tests") {
  test_components = [
    ":singleton-display-integration-test",
    ":display-ownership-integration-test",
  ]
  test_specs = {
    environments = vulkan_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
