# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("cpu_accessible_config") {
  defines = [ "CPU_ACCESSIBLE_VMO=1" ]
}

template("engine_library") {
  source_set(target_name) {
    forward_variables_from(invoker, "*")
    sources = [
      "color_conversion_state_machine.cc",
      "color_conversion_state_machine.h",
      "display_compositor.cc",
      "display_compositor.h",
      "engine.cc",
      "engine.h",
      "engine_types.cc",
      "engine_types.h",
      "release_fence_manager.cc",
      "release_fence_manager.h",
    ]

    public_deps = [
      "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_cpp",
      "//sdk/fidl/fuchsia.hardware.display.types:fuchsia.hardware.display.types_cpp",
      "//sdk/fidl/fuchsia.math:fuchsia.math_cpp",
      "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_cpp_hlcpp_conversion",
      "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_cpp_hlcpp_conversion",
      "//sdk/lib/inspect/component/cpp",
      "//src/lib/fxl",
      "//src/ui/scenic/lib/display",
      "//src/ui/scenic/lib/display:display",
      "//src/ui/scenic/lib/flatland",
      "//src/ui/scenic/lib/flatland/renderer",
      "//src/ui/scenic/lib/scheduling:frame_scheduler_interface",
      "//src/ui/scenic/lib/view_tree:view_tree_snapshot",
    ]

    deps = [
      "//sdk/lib/fidl/hlcpp",
      "//sdk/lib/sys/cpp",
    ]

    public_configs = []

    if (make_cpu_accessible) {
      public_configs += [ ":cpu_accessible_config" ]
    }
  }
}

# The default engine will not have render buffers be host-readable
# for performance.
engine_library("engine") {
  make_cpu_accessible = false
}

# For tests where we need the framebuffers to be host-readable so
# that we can inspect the pixels, use this build configuration.
engine_library("engine_host_accessible_framebuffers") {
  make_cpu_accessible = true
}
