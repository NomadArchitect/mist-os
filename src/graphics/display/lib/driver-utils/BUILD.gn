# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

source_set("driver-utils") {
  public = [
    "poll-until.h",
    "post-task.h",
    "scoped-value-change.h",
  ]
  sources = [ "scoped-value-change.cc" ]
  public_deps = [
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

test("driver-utils-test") {
  output_name = "display-driver-utils-test"
  sources = [
    "poll-until-test.cc",
    "post-task-test.cc",
    "scoped-value-change-test.cc",
  ]
  deps = [
    ":driver-utils",
    "//sdk/lib/async-loop:async-loop-default",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("driver-utils-test-package") {
  package_name = "display-driver-utils-test"
  deps = [ ":driver-utils-test" ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":driver-utils-test-package" ]
}
