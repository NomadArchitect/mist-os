# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/cc_test_executable.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":fake-display-coordinator-connector-integration-test",
    ":fake-display-coordinator-connector-unittest",
  ]
}

cc_test_executable("fake-display-coordinator-connector-unittest-bin") {
  output_name = "fake-display-coordinator-connector-unittest"
  testonly = true

  sources = [ "fake-display-coordinator-connector-unittest.cc" ]

  deps = [
    "//sdk/lib/fidl/hlcpp",
    "//src/graphics/display/testing/fake-coordinator-connector:service",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("fake-display-coordinator-connector-unittest") {
  manifest = "meta/fake-display-coordinator-connector-unittest.cml"
  deps = [ ":fake-display-coordinator-connector-unittest-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_type = "vulkan"
}

test("fake-display-coordinator-connector-integration-test-bin") {
  output_name = "fake-display-coordinator-connector-integration-test"
  sources = [ "fake-display-coordinator-connector-integration-test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/fidl/hlcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_component(
    "fake-display-coordinator-connector-integration-test-component") {
  testonly = true
  component_name = "fake-display-coordinator-connector-integration-test"
  manifest = "meta/fake-display-coordinator-connector-integration-test.cml"
  deps = [ ":fake-display-coordinator-connector-integration-test-bin" ]
}

fuchsia_test_package("fake-display-coordinator-connector-integration-test") {
  test_components =
      [ ":fake-display-coordinator-connector-integration-test-component" ]
  deps = [
    "//src/graphics/display/testing/fake-coordinator-connector:child-component",
  ]
}
