# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(target_cpu == "arm64", "The amlogic-clk driver is only valid on arm64.")

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":amlogic-clk-test" ]
}

driver_bind_rules("aml-clk-bind") {
  rules = "meta/aml-clk.bind"
  bind_output = "amlogic-clk.bindbc"

  tests = "meta/bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("amlogic-clk-driver-source") {
  output_name = "amlogic-clk"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "aml-a1-blocks.h",
    "aml-a5-blocks.h",
    "aml-axg-blocks.h",
    "aml-clk-blocks.h",
    "aml-clk-blocks.h",
    "aml-clk.cc",
    "aml-clk.h",
    "aml-fclk-rates.cc",
    "aml-fclk.h",
    "aml-g12a-blocks.h",
    "aml-g12b-blocks.h",
    "aml-gxl-blocks.h",
    "aml-sm1-blocks.h",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
    "//sdk/fidl/fuchsia.hardware.clock.measure:fuchsia.hardware.clock.measure_cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zircon-internal",
  ]
  deps = [
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("amlogic-clk-driver") {
  output_name = "amlogic-clk"
  deps = [
    ":aml-clk-bind",
    ":amlogic-clk-driver-source",
  ]
}

fuchsia_driver_component("amlogic-clk") {
  component_name = "amlogic-clk"
  deps = [ ":amlogic-clk-driver" ]
  info = "meta/amlogic-clk-info.json"
  manifest = "meta/amlogic-clk.cml"
}

fuchsia_driver_package("package") {
  package_name = "amlogic-clk"
  driver_components = [ ":amlogic-clk" ]
}

test("amlogic-clk-test-bin") {
  output_name = "amlogic-clk-test"
  sources = [ "aml-clk-test.cc" ]
  deps = [
    ":amlogic-clk-driver-source",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mmio:test_helper",
    "//src/devices/testing/no_ddk",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("amlogic-clk-test") {
  deps = [
    ":aml-clk-bind_test",
    ":amlogic-clk-test-bin",
  ]
}
