# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/component_manifest_resources.gni")
import("//build/toolchain/zircon/user_basic_redirect.gni")

user_basic_redirect("modules.basic") {
  testonly = true
  public_deps = [ ":modules" ]
}

group("modules") {
  testonly = true
  deps = [
    ":fake_driver_host",
    ":fake_root_driver",
  ]
}

static_library("entry_point") {
  public = [ "entry_point.h" ]
  sources = [ "entry_point.cc" ]
  public_deps = [
    "//src/zircon/lib/zircon",
    "//zircon/system/public",
  ]
}

executable("fake_driver_host") {
  output_name = "fake_driver_host"
  sources = [ "fake_driver_host.cc" ]
  deps = [
    ":dh-deps-a",
    ":entry_point",
  ]

  # The user.basic environment doesn't use toolchain-provided instrumentation
  # runtimes that wouldn't be compatible.  But it supports variants like ubsan
  # that require plumbing a custom runtime, which isn't handled for this
  # experimental test stub binary.
  exclude_toolchain_tags = [ "custom-runtime" ]
}

shared_library("dh-deps-a") {
  sources = [ "dh-deps-a.cc" ]
  deps = [
    ":dh-deps-b",
    ":dh-deps-c",
    "//zircon/system/public",
  ]
}

shared_library("dh-deps-b") {
  sources = [ "dh-deps-b.cc" ]
  deps = [ "//zircon/system/public" ]
}

shared_library("dh-deps-c") {
  sources = [ "dh-deps-c.cc" ]
  deps = [ "//zircon/system/public" ]
}

loadable_module("fake_root_driver") {
  sources = [ "fake_root_driver.cc" ]
  public = [ "driver_entry_point.h" ]
  deps = [ ":fake_root_driver_deps" ]
}

shared_library("fake_root_driver_deps") {
  sources = [ "fake_root_driver_deps.cc" ]
  deps = [ "//zircon/system/public" ]
}
