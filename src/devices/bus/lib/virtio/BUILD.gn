# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/components.gni")
import("//build/cpp/library_headers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":virtio",
    ":virtio-backend-test-package",
  ]
}

test("virtio-backend-test") {
  sources = [ "backend_tests.cc" ]
  deps = [
    ":virtio",
    "//sdk/lib/async-default",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//src/devices/pci/testing:pci-protocol-fake",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("virtio-backend-test-package") {
  component_name = "virtio-backend-test"
  deps = [ ":virtio-backend-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

library_headers("virtio-headers") {
  headers = [
    "lib/virtio/driver_utils.h",
    "lib/virtio/device.h",
    "lib/virtio/ring.h",
    "lib/virtio/backends/backend.h",
    "lib/virtio/backends/fake.h",
    "lib/virtio/backends/pci.h",
  ]
  public_deps = [
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/virtio",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
  visibility = [ ":virtio-common" ]
}

source_set("virtio-common") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "backends/pci.cc",
    "backends/pci_legacy.cc",
    "backends/pci_modern.cc",
    "device.cc",
    "driver_utils.cc",
    "ring.cc",
  ]
  public_deps = [
    ":virtio-headers",
    "//src/graphics/display/lib/driver-framework-migration-utils/logging:zxlogf",
  ]
  deps = [
    "//sdk/lib/stdcompat",
    "//src/lib/ddk:ddk-deprecated-mmio-headers",
    "//zircon/system/ulib/pretty",
  ]

  # TODO(https://fxbug.dev/42136089): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

group("virtio") {
  public_deps = [
    ":virtio-common",
    "//src/devices/lib/driver",
    "//src/graphics/display/lib/driver-framework-migration-utils/logging:logging-dfv1",
  ]
}

group("virtio-dfv2") {
  public_deps = [
    ":virtio-common",
    "//src/graphics/display/lib/driver-framework-migration-utils/logging:logging-dfv2",
  ]
}
