# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/components.gni")
import("//build/test.gni")

# Common serialutil functionality.
source_set("common") {
  sources = [
    "serialutil.cc",
    "serialutil.h",
  ]
  public_deps =
      [ "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_cpp" ]
  deps = [ "//sdk/lib/component/incoming/cpp" ]
}

# Definition for the `serialutil` binary.
executable("serialutil") {
  visibility = [ ":*" ]
  sources = [ "main.cc" ]
  deps = [ ":common" ]
}

# Configuration that is used to package `serialutil` into assembled images.
bootfs_files_for_assembly("bootfs") {
  deps = [ ":serialutil" ]
}

# Tests for the `serialutil` program.
test("serialutil-test-bin") {
  output_name = "serialutil-test"
  sources = [ "serialutil-test.cc" ]
  include_dirs = [ "../../drivers/serial" ]
  deps = [
    ":common",
    "../../drivers/serial:common",
    "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_cpp",
    "//sdk/fidl/fuchsia.hardware.serialimpl:fuchsia.hardware.serialimpl_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
  ]
}

# Package used for executing the `serialutil` tests.
fuchsia_unittest_package("serialutil-test") {
  deps = [ ":serialutil-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "INFO"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":serialutil-test" ]
}
