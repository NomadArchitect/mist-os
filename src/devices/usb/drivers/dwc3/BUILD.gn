# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/export_fuchsia_package_to_bazel.gni")
import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("dwc3_bind") {
  rules = "meta/dwc3.bind"
  bind_output = "dwc3.bindbc"
  tests = "meta/tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_bindlib",
    "//src/devices/bind/fuchsia.designware.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("dwc3-common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "dwc3-commands.cc",
    "dwc3-endpoints.cc",
    "dwc3-ep0.cc",
    "dwc3-events.cc",
    "dwc3.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.usb.endpoint:fuchsia.hardware.usb.endpoint_cpp",
    "//sdk/lib/driver/component/cpp",
    "//sdk/lib/driver/logging/cpp",
    "//sdk/lib/fit",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.designware.platform:fuchsia.designware.platform_cpp",
    "//src/devices/usb/lib/usb:usb-dfv2-compat-logging",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/platform-device/cpp",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/mmio",
    "//src/devices/usb/lib/usb-endpoint:usb-endpoint-server-dfv2-compat-logging",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_cc_driver("dwc3-driver") {
  output_name = "dwc3"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":dwc3-common",
    ":dwc3_bind",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
  ]
}

fuchsia_driver_component("dwc3") {
  info = "meta/dwc3-info.json"
  component_name = "dwc3"
  deps = [ ":dwc3-driver" ]
  manifest = "meta/dwc3.cml"
}

fuchsia_driver_package("package") {
  package_name = "dwc3"
  driver_components = [ ":dwc3" ]
}

test("dwc3-test-bin") {
  configs += [ "//build/config:all_source" ]
  sources = [ "dwc3-test.cc" ]
  output_name = "dwc3-test"
  deps = [
    ":dwc3-common",
    ":dwc3_bind",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/fake-platform-device/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/testing/fake-mmio-reg",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("dwc3-test") {
  deps = [ ":dwc3-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":dwc3-test",
    ":dwc3_bind_test",
  ]
}

export_fuchsia_package_to_bazel("package_for_bazel") {
  package = ":package"
}
