# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/export_fuchsia_package_to_bazel.gni")
import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("dwc3_bind") {
  rules = "meta/dwc3.bind"
  bind_output = "dwc3.bindbc"
  tests = "meta/tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_bindlib",
    "//src/devices/bind/fuchsia.designware.platform",
    "//src/devices/bind/fuchsia.devicetree",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("dwc3-driver") {
  output_name = "dwc3"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "dwc3-commands.cc",
    "dwc3-endpoints.cc",
    "dwc3-ep0.cc",
    "dwc3-events.cc",
    "dwc3.cc",
  ]
  deps = [
    ":dwc3_bind",
    "//sdk/fidl/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.endpoint:fuchsia.hardware.usb.endpoint_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/fit",
    "//src/devices/bin/driver_runtime",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.designware.platform:fuchsia.designware.platform_cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb-endpoint:usb-endpoint-server",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_driver_component("dwc3") {
  info = "meta/dwc3-info.json"
  component_name = "dwc3"
  deps = [ ":dwc3-driver" ]
  manifest = "meta/dwc3.cml"
}

fuchsia_driver_package("package") {
  package_name = "dwc3"
  driver_components = [ ":dwc3" ]
}

test("dwc3-test-bin") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "dwc3-commands.cc",
    "dwc3-endpoints.cc",
    "dwc3-ep0.cc",
    "dwc3-events.cc",
    "dwc3-test.cc",
    "dwc3.cc",
  ]
  output_name = "dwc3-test"
  deps = [
    ":dwc3_bind",
    "//sdk/fidl/fuchsia.hardware.usb.dci:fuchsia.hardware.usb.dci_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.endpoint:fuchsia.hardware.usb.endpoint_cpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//sdk/lib/fit",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.designware.platform:fuchsia.designware.platform_cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/mock-ddk",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb-endpoint:usb-endpoint-server",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("dwc3-test") {
  deps = [ ":dwc3-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":dwc3-test",
    ":dwc3_bind_test",
  ]
}

export_fuchsia_package_to_bazel("package_for_bazel") {
  package = ":package"
}
