# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/cc_test_executable.gni")

source_set("analysis") {
  sources = [
    "analysis.cc",
    "analysis.h",
    "dropout.h",
    "generators.h",
    "glitch.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.media:fuchsia.media_hlcpp",
    "//sdk/lib/syslog/cpp",
    "//src/media/audio/lib/format",
  ]

  deps = [
    "//src/lib/fxl",
    "//src/media/audio/lib/wav",
    "//zircon/system/ulib/fbl",
  ]
}

cc_test_executable("unittest-bin") {
  visibility = [ ":*" ]
  testonly = true
  output_name = "audio_lib_analysis_unittests"

  sources = [
    "analysis_unittest.cc",
    "dropout_unittest.cc",
    "generators_unittest.cc",
    "glitch_unittest.cc",
  ]

  deps = [
    ":analysis",
    "//src/lib/fxl/test:gtest_main",
    "//src/zircon/lib/zircon",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_unittest_package("audio_lib_analysis_unittests") {
  deps = [ ":unittest-bin" ]

  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":audio_lib_analysis_unittests" ]
}
