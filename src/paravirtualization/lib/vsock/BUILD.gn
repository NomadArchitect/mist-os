# Copyright 2024 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test.gni")
import("//build/components/fuchsia_test_component.gni")
import("//build/components/fuchsia_test_package.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":vsock_test" ]
}

source_set("vsock") {
  public = [
    "socket.h",
    "vm_sockets.h",
  ]
  sources = [
    "socket.cc",
    "zxio_connected_socket.cc",
    "zxio_connected_socket.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.vsock:fuchsia.vsock_cpp",
    "//zircon/system/ulib/fbl",
  ]

  deps = [
    "//sdk/lib/async:async-cpp",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/zxio",
  ]
}

test("vsock_test_bin") {
  sources = [ "socket_create_test.cc" ]
  deps = [
    ":vsock",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

fuchsia_test_component("vsock_test_component") {
  manifest = "meta/vsock_test.cml"
  component_name = "vsock_test"
  deps = [ ":vsock_test_bin" ]
}

fuchsia_test_package("vsock_test") {
  test_components = [ ":vsock_test_component" ]
  subpackages = [ "//src/paravirtualization/testing:vsock-for-test" ]
}
