# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  edition = "2021"
  output_name = "bt_snoop"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.bluetooth.snoop:fuchsia.bluetooth.snoop_rust",
    "//sdk/fidl/fuchsia.hardware.bluetooth:fuchsia.hardware.bluetooth_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/rust/zx",
    "//src/lib/diagnostics/inspect/format/rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-sync",
    "//src/lib/trace/rust:trace",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:byteorder",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:log",
  ]

  test_deps = [
    "//src/lib/async-utils",
    "//src/lib/diagnostics/testing/diagnostics-assertions/rust:diagnostics-assertions",
  ]

  sources = [
    "src/bounded_queue.rs",
    "src/main.rs",
    "src/packet_logs.rs",
    "src/snooper.rs",
    "src/subscription_manager.rs",
    "src/tests.rs",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_package_with_single_component("bt-snoop") {
  deps = [ ":bin" ]
  manifest = "meta/bt-snoop.cml"
}

fuchsia_unittest_package("bt-snoop-unittests") {
  deps = [ ":bin_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":bt-snoop-unittests" ]
}
