# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/testing/cc_test_executable.gni")

bind_test("wlanif_bind_test") {
  rules = "//src/connectivity/wlan/drivers/wlanif/meta/wlanif.bind"
  tests = "meta/wlanif_bind_test.json"
  deps = [ "//sdk/fidl/fuchsia.wlan.fullmac:fuchsia.wlan.fullmac_bindlib" ]
}

cc_test_executable("startup_shutdown_unittest") {
  output_name = "wlanif_startup_shutdown_unittest"
  testonly = true

  sources = [ "startup_shutdown_unittest.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/fidl/fuchsia.wlan.fullmac:fuchsia.wlan.fullmac_cpp",
    "//sdk/fidl/fuchsia.wlan.sme:fuchsia.wlan.sme_cpp",
    "//sdk/lib/async_patterns/testing/cpp:cpp",
    "//sdk/lib/driver/testing/cpp:cpp",
    "//sdk/lib/sys/cpp/testing:cpp",
    "//src/connectivity/wlan/drivers/wlanif:lib_source",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_package("wlanif_startup_shutdown_unittest") {
  deps = [ ":startup_shutdown_unittest" ]
  test_specs = {
    # Some tests return errors to MLME's FIDL requests on startup.
    # In those cases, MLME will emit error logs.
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
group("tests") {
  testonly = true
  deps = [
    ":wlanif_bind_test",
    ":wlanif_startup_shutdown_unittest",
  ]
}
