# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")

bind_test("wlanif_bind_test") {
  rules = "//src/connectivity/wlan/drivers/wlanif/meta/wlanif.bind"
  tests = "meta/wlanif_bind_test.json"
  deps = [ "//sdk/fidl/fuchsia.wlan.fullmac:fuchsia.wlan.fullmac_bindlib" ]
}

executable("startup_shutdown_unittest") {
  output_name = "wlanif_startup_shutdown_unittest"
  testonly = true

  sources = [ "startup_shutdown_unittest.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_cpp",
    "//sdk/fidl/fuchsia.wlan.fullmac:fuchsia.wlan.fullmac_cpp",
    "//sdk/lib/async_patterns/testing/cpp:cpp",
    "//sdk/lib/driver/testing/cpp:cpp",
    "//sdk/lib/sys/cpp/testing:cpp",
    "//src/connectivity/wlan/drivers/wlanif:lib_source_mlme_stub",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_unittest_package("wlanif_unittest") {
  deps = [ ":startup_shutdown_unittest" ]
  test_specs = {
    # Some tests mock MLME failing to start. In those cases, wlanif will emit error logs.
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":wlanif_bind_test",
    ":wlanif_unittest",
  ]
}
