# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//src/starnix/build/starnix_linux_rustc_binary.gni")
import("//src/starnix/kernel/starnix.gni")

group("tests") {
  testonly = true

  # TODO(https://fxbug.dev/376749368) do stack gluing on riscv
  if (target_cpu != "riscv64") {
    deps = [ ":starnix_fp_stack_glue_test" ]
  }
}

rustc_test("bin") {
  testonly = true
  edition = "2021"
  output_name = "starnix_fp_stack_glue_test"
  sources = [ "src/lib.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.buildinfo:fuchsia.buildinfo_rust",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//sdk/rust/zx",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-runtime",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_test_component("integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":bin" ]
  test_type = "starnix"
}

starnix_linux_rustc_binary("print_uname_bin") {
  testonly = true
  edition = 2021
  source_root = "src/print_uname.rs"
  sources = [ source_root ]
  deps = [ "//third_party/rust_crates:nix" ]
}

fuchsia_component("print_uname") {
  testonly = true
  manifest = "meta/print_uname.cml"
  deps = [ ":print_uname_bin" ]
}

fuchsia_component("realm") {
  testonly = true
  manifest = "meta/realm.cml"
}

fuchsia_test_package("starnix_fp_stack_glue_test") {
  test_components = [ ":integration_test" ]

  deps = [
    ":print_uname",
    ":realm",
  ]
  subpackages = [
    "//src/starnix/containers/debian:debian_package",
    "//src/starnix/kernel:starnix_kernel_package",
  ]
}
