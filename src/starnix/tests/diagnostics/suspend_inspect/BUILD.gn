# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//src/starnix/build/starnix_linux_executable.gni")
import("//src/starnix/kernel/starnix.gni")

group("suspend_inspect") {
  testonly = true
  deps = [ ":tests" ]
}

group("tests") {
  testonly = true
  deps = [
    ":starnix_suspend_inspect_test",
    ":starnix_suspend_inspect_test_root",
    "testing/suspend_driver_realm_proxy:pkg",
  ]
}

rustc_test("bin") {
  testonly = true
  edition = "2021"
  output_name = "starnix_suspend_inspect_test"
  sources = [ "src/main.rs" ]
  source_root = "src/main.rs"
  deps = [
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/devices/testing/syscall-intercept:fuchsia.test.syscalls_rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-fs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/starnix/tests/diagnostics/suspend_inspect/testing/suspend_driver_realm_proxy/fidl/fuchsia.test.suspend:fuchsia.test.suspend_rust",
    "//src/sys/lib/component-events",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_test_component("integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":bin" ]

  # TODO(https://fxbug.dev/42081099) remove once ScopedInstance can be used separately
  check_includes = false
}

starnix_linux_executable("suspend_linux") {
  testonly = true
  sources = [ "src/suspend_linux.cc" ]
  deps = [ "//src/lib/files" ]
}

fuchsia_component("container") {
  testonly = true
  manifest = "//src/starnix/containers/debian/meta/debian_container.cml"
}

fuchsia_component("suspend_client") {
  testonly = true
  manifest = "meta/suspend_client.cml"
  deps = [ ":suspend_linux" ]
}

fuchsia_package("starnix_suspend_inspect_test") {
  testonly = true
  deps = [
    ":container",
    ":integration_test",
    ":suspend_client",
    "//src/starnix/containers/debian:container_resources",
  ]
  subpackages = [
    "//src/starnix/kernel:starnix_kernel_package",
    "//src/power/broker:package",
    "//src/power/system-activity-governor:package",
  ]
}

fuchsia_test_component("starnix_suspend_inspect_test_root_component") {
  component_name = "starnix_suspend_inspect_test_root"
  testonly = true
  manifest = "meta/integration_test_root.cml"
  test_type = "system"
}

fuchsia_test_package("starnix_suspend_inspect_test_root") {
  test_components = [ ":starnix_suspend_inspect_test_root_component" ]
  subpackages = [
    ":starnix_suspend_inspect_test",
    "testing/suspend_driver_realm_proxy:pkg",
  ]
}
