# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/starnix/tests/build/starnix_linux_test_component.gni")
import("//src/starnix/tests/starnix_test_subpackages.gni")

group("tests") {
  testonly = true
  deps = [ ":package" ]
}

source_set("test-helpers") {
  testonly = true
  sources = [ "test-helpers.cc" ]

  public_deps = [
    "//src/lib/files",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
  ]
}

executable("fake-suspend-controller-bin") {
  sources = [ "fake-suspend-controller.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.suspend:fuchsia.hardware.suspend_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/power/testing/fake-suspend:test.suspendcontrol_cpp",
  ]
}

fuchsia_component("success-fake-suspend-controller") {
  manifest = "meta/success-fake-suspend-controller.cml"
  deps = [ ":fake-suspend-controller-bin" ]
}

test("success-test-bin") {
  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
  sources = [ "success-test.cc" ]
  deps = [
    ":test-helpers",
    "//src/lib/fxl/test:gtest_main",
  ]
}

starnix_linux_test_component("success-test") {
  test_label = ":success-test-bin"
  test_binary = "success-test-bin"
  test_type = "starnix"
}

fuchsia_component("failure-fake-suspend-controller") {
  manifest = "meta/failure-fake-suspend-controller.cml"
  deps = [ ":fake-suspend-controller-bin" ]
}

test("failure-test-bin") {
  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
  sources = [ "failure-test.cc" ]
  deps = [
    ":test-helpers",
    "//src/lib/fxl/test:gtest_main",
  ]
}

starnix_linux_test_component("failure-test") {
  test_label = ":failure-test-bin"
  test_binary = "failure-test-bin"
  test_type = "starnix"
}

fuchsia_test_package("package") {
  package_name = "starnix-sysfs-power-state-integration-test"
  test_components = [
    ":success-test",
    ":failure-test",
  ]
  deps = [
    ":failure-fake-suspend-controller",
    ":success-fake-suspend-controller",
  ]

  subpackages = starnix_test_subpackages
  subpackages += [
    "//src/starnix/containers/debian:debian_package",
    "//src/power/testing/fake-suspend:fake-suspend-pkg",
  ]
}
