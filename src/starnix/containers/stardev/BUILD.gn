# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/packages/prebuilt_package.gni")

declare_args() {
  # The path to a locally built stardev.tar package.
  stardev_path = ""
}

group("stardev") {
  if (stardev_path != "") {
    deps = [ ":pkg" ]
  }
}

if (stardev_path != "") {
  compiled_action("stardev_far") {
    tool = "//src/starnix/tools/convert_tarball_to_starnix_container:convert_tarball_to_starnix_container_bin"
    tool_output_name = "convert_tarball_to_starnix_container"

    sources = [ stardev_path ]
    outputs = [ "$target_out_dir/stardev.far" ]

    args = [
      "--features",
      "rootfs_rw",
      "--input-format",
      "docker-archive",
      rebase_path(sources[0], root_build_dir),
      rebase_path(outputs[0], root_build_dir),
    ]
  }

  prebuilt_package("pkg") {
    package_name = "stardev"
    archive = "$target_out_dir/stardev.far"

    deps = [ ":stardev_far" ]
  }
}
