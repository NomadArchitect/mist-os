# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # The target API level of the current build.
  #
  # For the default platform build, the API level is "PLATFORM".
  #
  # If this is _not_ set to "PLATFORM", then it must be set to a positive
  # integer corresponding to a currently Supported (not Sunset) API level. In
  # that case, the build will target the given API level.
  #
  # This is intended for use with code that is included in IDK sub-builds. Not
  # all targets support the non-default value, and other uses are unsupported.
  current_build_target_api_level = "PLATFORM"

  # Deprecated name for the variable above that is still used by obsolete bots.
  # TODO(https://fxbug.dev/330709069): Remove after turning down the
  # core.x64-sdk_source_sets_and_shlibs-api*-build_only bots.
  override_target_api_level = false
}

if (override_target_api_level != false) {
  print(
      "Obsolete GN arg `override_target_api_level` specified. Replace with `current_build_target_api_level`.")
  assert(current_build_target_api_level == "PLATFORM")
  current_build_target_api_level = override_target_api_level
}

# Numerical values associated with special API levels, as established by
# RFC-0246.
_FUCHSIA_PLATFORM_VALUE = 4293918720

if (current_build_target_api_level == "PLATFORM") {
  clang_fuchsia_api_level = _FUCHSIA_PLATFORM_VALUE

  # Directory name to use for target API level.
  target_cpu_dir_name_for_target_api_level = target_cpu

  # Base path for library binaries in SDKs.
  sdk_prebuilt_base_for_target_api_level =
      "arch/${target_cpu_dir_name_for_target_api_level}"
} else {
  # Ensure that `current_build_target_api_level` is an integer corresponding to a
  # normal API level.
  assert(
      0 < current_build_target_api_level,
      "Override must be 'PLATFORM' or a positive integer, not: ${current_build_target_api_level}")
  assert(current_build_target_api_level < 2147483648,
         "Special API levels should be given by name, not number.")

  clang_fuchsia_api_level = current_build_target_api_level

  target_cpu_dir_name_for_target_api_level =
      "${target_cpu}-api-${current_build_target_api_level}"
  sdk_prebuilt_base_for_target_api_level =
      "obj/${target_cpu_dir_name_for_target_api_level}"
}
