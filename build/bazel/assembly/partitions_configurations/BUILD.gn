# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import(
    "//build/bazel/assembly/partitions_configurations/verify_partitions_configuration.gni")
import("//build/board.gni")

group("verifications") {
  testonly = true
  deps = []

  if (target_cpu == "x64") {
    # Avoid building ESP in other configurations.
    if (partitions_config_label == "//boards/partitions:x64") {
      deps += [ ":verify_x64" ]
    }
  }

  if (target_cpu == "arm64") {
    deps += [ "//boards/partitions/vim3:verify" ]
  }

  if (target_cpu == "riscv64") {
    deps += [ ":verify_riscv64" ]
  }
}

if (target_cpu == "x64") {
  verify_partitions_configuration("verify_x64") {
    testonly = true
    gn_partitions_config_target = "//boards/partitions:x64"
    bazel_partitions_config_target = ":x64"
    bazel_inputs = [ "//build/images/flash:esp.bazel_input" ]
  }
}

if (target_cpu == "riscv64") {
  verify_partitions_configuration("verify_riscv64") {
    testonly = true
    gn_partitions_config_target = "//boards/partitions:riscv64"
    bazel_partitions_config_target = ":riscv64"
  }
}
