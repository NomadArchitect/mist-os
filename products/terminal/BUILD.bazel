# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

load("@fuchsia_build_info//:args.bzl", "authorized_ssh_keys_label")
load(
    "@fuchsia_sdk//fuchsia:assembly.bzl",
    "ARCH",
    "BUILD_TYPES",
    "INPUT_DEVICE_TYPE",
    "fuchsia_elf_sizes",
    "fuchsia_product",
    "fuchsia_product_bundle",
    "fuchsia_product_configuration",
    "fuchsia_virtual_device",
)
load("//build/info:info.bzl", "DEFAULT_PRODUCT_BUILD_INFO")

package(default_visibility = ["//visibility:public"])

_PRODUCT_CONFIG_BASE_JSON = {
    "platform": {
        "build_type": BUILD_TYPES.ENG,
        "bluetooth": {
            "type": "standard",
            "snoop": "lazy",
        },
        "development_support": {
            "include_sl4f": True,
            "authorized_ssh_keys_path": "LABEL(%s)" % authorized_ssh_keys_label if authorized_ssh_keys_label else None,
            "tools": {
                "connectivity": {
                    "enable_networking": True,
                    "enable_wlan": True,
                    "enable_thread": True,
                },
            },
        },
        "fonts": {
            "font_collection": "open-fonts-collection",
        },
        "media": {
            "audio": {
                "full_stack": {},
            },
            "enable_codecs": True,
            "enable_sessions": True,
        },
        "session": {
            "enabled": True,
            # Avoid impacting existing terminal users like chromium tests.
            "autolaunch": False,
        },
        "storage": {
            "mutable_storage_garbage_collection": True,
        },
        "ui": {
            "enabled": True,
            "supported_input_devices": [
                INPUT_DEVICE_TYPE.BUTTON,
                INPUT_DEVICE_TYPE.TOUCHSCREEN,
            ],
            "with_synthetic_device_support": True,
        },
    },
    "product": {
        "build_info": DEFAULT_PRODUCT_BUILD_INFO,
        "session": {
            "url": "fuchsia-pkg://fuchsia.com/tiles-session#meta/tiles-session.cm",
        },
    },
}

_PRODUCT_BASE_PACKAGES = [
    "//src/fonts/asset_providers:open-fonts-collection",
]

_PRODUCT_CACHE_PACKAGES = [
    "//src/ui/bin/terminal",
    "//src/ui/bin/tiles-session",
    # Used to mock out display hardware in UI integration tests.
    "//src/graphics/display/testing/fake-coordinator-connector",
    # Add validator package for use in tests.
    # TODO(https://fxbug.dev/42077782): Remove once we support packages in the
    # SDK.
    "//src/diagnostics/validator/inspect:validator",
    # Add test validation packages for use in tests.
    # TODO(https://fxbug.dev/42077782): Remove once we support packages in the
    # SDK and we want to put this in the SDK.
    "//src/tests/fidl/dynsuite/client_suite/harness",
    "//src/tests/fidl/dynsuite/server_suite/harness",
    # Fake services needed by Chromium tests until subpackages are supported OOT
    # and fakes are provided with the SDK. See crbug.com/1326674.
    # TODO(crbug.com/1408597): Remove once the Chromium tests package the fakes.
    "//src/developer/build_info/testing:fake-build-info",
    # archivist-for-embedding is needed by
    # WebEngineIntegrationLoggingTest.SetJavaScriptLogLevel_DEBUG.
    # TODO(crbug.com/1451376): Move it to a peer group as appropriate.
    "//src/diagnostics/archivist:archivist-for-embedding",
    "//src/testing/fidl/intl_property_manager",
    # A font provider with built-in font file assets.
    # TODO(crbug.com/1408597): Once OOT fakes are available, this can be
    # turned into a hermetic component, or a subpackage.
    "//src/fonts:fonts_hermetic_for_test",
    # A font provider without built-in font file assets.
    "//src/fonts:fonts_configurable_for_test",
    # Add Test UI Stack for use in tests.
    # TODO(https://fxbug.dev/42056298): Remove once we migrate client usage to
    # subpackages.
    "//src/ui/testing/test_ui_stack:flatland-scene-manager-test-ui-stack",
    "//src/ui/testing/test_ui_stack:test-ui-stack",
    # Add FIDL compatibility test harneses for SDK customers to use until these
    # are in the SDK for subpackaging
    #
    # TODO(https://fxbug.dev/42077782): Remove once we support packages in the
    # SDK and we want to put this in the SDK.
    "//src/tests/fidl/compatibility:fidl-compatibility-array-harness_pkg",
    "//src/tests/fidl/compatibility:fidl-compatibility-minimal-harness_pkg",
    "//src/tests/fidl/compatibility:fidl-compatibility-struct-harness_pkg",
    "//src/tests/fidl/compatibility:fidl-compatibility-table-harness_pkg",
    "//src/tests/fidl/compatibility:fidl-compatibility-union-harness_pkg",
    "//src/tests/fidl/compatibility:fidl-compatibility-vector-harness_pkg",
    "//src/tests/fidl/compatibility/cpp",
    "//src/tests/fidl/compatibility/hlcpp",
    "//src/tests/fidl/compatibility/llcpp",
    "//src/tests/fidl/compatibility/go",
    "//src/tests/fidl/compatibility/rust",
]

fuchsia_product_configuration(
    name = "product_config",
    base_packages = _PRODUCT_BASE_PACKAGES,
    cache_packages = _PRODUCT_CACHE_PACKAGES,
    product_config_json = _PRODUCT_CONFIG_BASE_JSON,
)

# TODO(https://fxbug.dev/370543906): Remove this product once it's no longer
# needed.
fuchsia_product_configuration(
    name = "product_config_with_netstack2",
    base_packages = _PRODUCT_BASE_PACKAGES,
    cache_packages = _PRODUCT_CACHE_PACKAGES,
    product_config_json = {
        "platform": _PRODUCT_CONFIG_BASE_JSON["platform"] | {
            "connectivity": {
                "network": {
                    "netstack_version": "netstack2",
                },
            },
        },
        "product": _PRODUCT_CONFIG_BASE_JSON["product"],
    },
)

[
    [
        (
            fuchsia_product(
                name = "image{}.{}".format(
                    suffix,
                    board + variant,
                ),
                board_config = "//boards:{}".format(board + variant),
                legacy_bundle = "//build/bazel/assembly/assembly_input_bundles:legacy_fuchsia",
                platform_artifacts = "//build/bazel/assembly/assembly_input_bundles:platform_eng",
                product_config = ":product_config",
            ),
            fuchsia_elf_sizes(
                name = "elf_sizes{}.{}".format(
                    suffix,
                    board + variant,
                ),
                product = ":image{}.{}".format(
                    suffix,
                    board + variant,
                ),
            ),
            fuchsia_product_bundle(
                name = "product_bundle{}.{}".format(
                    suffix,
                    board + variant,
                ),
                board_name = board,
                default_virtual_device = "//build/bazel/assembly/virtual_devices:{}-emu-recommended".format(arch),
                main = ":image{}.{}".format(
                    suffix,
                    board + variant,
                ),
                partitions_config = "//build/bazel/assembly/partitions_configurations:{}".format(board),
                product_bundle_name = "fuchsia",
                recovery = "//products/zedboot:{}".format(board + variant),
                repository_keys = "//src/sys/pkg/repositories/devhost/keys",
                update_epoch = "1",
                update_version_file = "//build/info:version",
                virtual_devices = [
                    "//build/bazel/assembly/virtual_devices:{}-emu-recommended".format(arch),
                    "//build/bazel/assembly/virtual_devices:{}-emu-min".format(arch),
                    "//build/bazel/assembly/virtual_devices:{}-emu-large".format(arch),
                ],
            ),
        )
        for suffix in [
            "",
            "_with_netstack2",
        ]
    ]
    for (board, arch, variant) in [
        ("x64", "x64", ""),
        ("x64", "x64", "_reduced_perf_variation"),
        ("arm64", "arm64", ""),
        ("vim3", "arm64", ""),
        ("vim3", "arm64", "_reduced_perf_variation"),
    ]
]
