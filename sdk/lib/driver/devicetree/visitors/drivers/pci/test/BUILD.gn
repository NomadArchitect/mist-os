# Copyright 2025 The Fuchsia Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_unittest_package.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":pci-devicetree-test" ]
}

dtb("crosvm_arm64_pci_golden_dtb") {
  sources = [ "dts/crosvm_arm64_pci_golden.dts" ]
  dtcflags = [ "-q" ]
}

dtb("qemu_arm64_pci_golden_dtb") {
  sources = [ "dts/qemu_arm64_pci_golden.dts" ]
  dtcflags = [ "-q" ]
}

resource("pci-dtb-files") {
  deps = [
    ":crosvm_arm64_pci_golden_dtb",
    ":qemu_arm64_pci_golden_dtb",
  ]
  sources = get_target_outputs(deps[0]) + get_target_outputs(deps[1])
  outputs = [ "test-data/{{source_file_part}}" ]
}

test("pci-devicetree-test-bin") {
  output_name = "pci_devicetree_test"
  sources = [ "pci-devicetree-test.cc" ]
  deps = [
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//sdk/lib/driver/devicetree/visitors/drivers/pci",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("pci-devicetree-test") {
  component_name = "pci-devicetree-test"
  deps = [
    ":pci-devicetree-test-bin",
    ":pci-dtb-files",
  ]
}
