# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":mailbox-visitor-test" ]
  testonly = true
}

devicetree_visitor("mailbox") {
  deps = [ ":mailbox-visitor" ]
}

source_set("mailbox-visitor") {
  sources = [
    "mailbox-visitor.cc",
    "mailbox-visitor.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.mailbox:fuchsia.hardware.mailbox_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.mailbox:fuchsia.hardware.mailbox_cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:common-types",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/bind/fuchsia.mailbox:fuchsia.mailbox_cpp",
  ]
  public_deps = [ "//sdk/lib/driver/devicetree/visitors:driver-visitor" ]
}

test("mailbox-visitor-test-bin") {
  sources = [ "test/mailbox-visitor-test.cc" ]
  deps = [
    ":mailbox-visitor",
    "//sdk/fidl/fuchsia.hardware.mailbox:fuchsia.hardware.mailbox_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.mailbox:fuchsia.hardware.mailbox_cpp",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//sdk/lib/driver/devicetree/visitors/default/mmio",
    "//src/devices/bind/fuchsia.mailbox:fuchsia.mailbox_cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("mailbox-dtb") {
  sources = [ "test/dts/mailbox.dts.S" ]
}

resource("mailbox-dtb-file") {
  sources = get_target_outputs(":mailbox-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":mailbox-dtb" ]
}

fuchsia_unittest_package("mailbox-visitor-test") {
  component_name = "mailbox-visitor-test"
  deps = [
    ":mailbox-dtb-file",
    ":mailbox-visitor-test-bin",
  ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
