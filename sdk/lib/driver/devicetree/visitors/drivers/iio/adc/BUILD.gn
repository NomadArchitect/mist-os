# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/devicetree/devicetree_visitor.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":adc-visitor-test" ]
  testonly = true
}

devicetree_visitor("adc-visitor") {
  deps = [ ":adc" ]
}

source_set("adc") {
  sources = [
    "adc-visitor.cc",
    "adc-visitor.h",
  ]
  deps = [ "//sdk/lib/driver/devicetree/visitors/drivers/iio/lib:iio" ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.adc:fuchsia.hardware.adc_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.adcimpl:fuchsia.hardware.adcimpl_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.adcimpl:fuchsia.hardware.adcimpl_cpp",
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.adc:fuchsia.adc_cpp",
  ]
}

test("adc-visitor-test-bin") {
  sources = [ "test/adc-visitor-test.cc" ]
  deps = [
    ":adc",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//sdk/lib/driver/devicetree/visitors/default/mmio",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("adc-dtb") {
  sources = [ "test/dts/adc.dts.S" ]
}

resource("adc-dtb-file") {
  sources = get_target_outputs(":adc-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":adc-dtb" ]
}

fuchsia_unittest_package("adc-visitor-test") {
  component_name = "adc-visitor-test"
  deps = [
    ":adc-dtb-file",
    ":adc-visitor-test-bin",
  ]
}
