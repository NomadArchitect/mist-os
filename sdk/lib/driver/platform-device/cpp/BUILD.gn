# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/cpp/sdk_source_set.gni")
import("//build/test.gni")

sdk_source_set("cpp") {
  category = "internal"
  sdk_name = "driver_platform_device_cpp"
  include_base = "//sdk"

  sources = [
    "pdev.cc",
    "pdev.h",
  ]

  non_sdk_deps = [ "//src/devices/power/lib/from-fidl/cpp" ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/lib/driver/power/cpp",
    "//src/devices/lib/mmio",
    "//zircon/system/ulib/zx",
  ]

  public_configs = [ "//sdk/config" ]
}

test("driver-platform-device-test") {
  sources = [ "pdev-test.cc" ]

  deps = [
    ":cpp",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp_testing",
    "//sdk/lib/async-default",
    "//sdk/lib/async-loop:async-loop-cpp",
    "//sdk/lib/async-loop:async-loop-default",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/lib/mmio:test_helper",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake-resource",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("package") {
  package_name = "driver-platform-device-test"
  deps = [ ":driver-platform-device-test" ]
}

group("tests") {
  testonly = true
  deps = [ ":package" ]
}
