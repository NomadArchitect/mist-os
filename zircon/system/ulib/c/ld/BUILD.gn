# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/zircon/lib/zircon/libzircon.gni")
import("//zircon/system/ulib/c/libc.gni")

# This is linked into the legacy libc.so to satisfy references from the old
# musl integrated dynamic linker code.
group("musl-glue") {
  deps = [
    ":musl-glue.basic",
    "//zircon/third_party/ulib/musl/src/thread:tls_get_addr",
  ]
}

# These things are called too early in startup to use the compiler ABI.
libc_source_set("musl-glue.basic") {
  basic_abi = true

  # These symbols are what the legacy libc.so gets from this libc_source_set().
  global_symbols = [
    "_dl_log_write",
    "_dl_log_write_init",
    "_dl_log_write_init_fallback",
    "_dl_phdr_report_globals",
  ]

  # All the vDSO's symbols are safe since it uses only the basic ABI anyway.
  undefined_symbols = libzircon_linkage_names

  # _dl_log_write's global state has destructor registration, though it takes
  # care to be constinit so it's guaranteed safe to use before constructors.
  allow_init = true
  undefined_symbols += [
    "__cxa_atexit",
    "__dso_handle",
  ]

  sources = [
    "_dl_log_write.cc",
    "_dl_phdr_report_globals.cc",
  ]
  deps = [
    "../stdlib:hermetic",
    "../string:hermetic",
    "//sdk/lib/ld:ld-log-zircon",
    "//src/lib/elfldltl:headers",
    "//zircon/system/ulib/c/stdio/printf_core:wrapper",
    "//zircon/system/ulib/fbl",
    "//zircon/third_party/ulib/musl:musl_internal",
  ]
}
