# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("cpp") {
  deps = [
    ":unmanaged_element_cpp",
    ":wake_lease_cpp",
  ]
}

static_library("unmanaged_element_cpp") {
  sources = [ "unmanaged_element.cc" ]
  public = [ "unmanaged_element.h" ]
  public_deps = [
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/lib/driver/power/cpp",
    "//sdk/lib/fit",
  ]
  deps = [ "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_cpp" ]
}

executable("unmanaged_element_unittest_cpp") {
  testonly = true
  sources = [ "unmanaged_element_unittest.cc" ]
  deps = [
    ":unmanaged_element_cpp",
    "testing",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/lib/async_patterns/cpp",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

fuchsia_unittest_component("unmanaged-element-unittest-cpp") {
  deps = [ ":unmanaged_element_unittest_cpp" ]
}

executable("unmanaged_element_integration_test_cpp") {
  testonly = true
  sources = [ "unmanaged_element_integration_test.cc" ]
  deps = [
    ":unmanaged_element_cpp",
    "testing",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/lib/diagnostics/reader/cpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

fuchsia_test_component("unmanaged-element-integration-test-cpp") {
  manifest = "meta/unmanaged_element_integration_test_cpp.cml"
  deps = [ ":unmanaged_element_integration_test_cpp" ]
}

static_library("wake_lease_cpp") {
  sources = [ "wake_lease.cc" ]
  public = [ "wake_lease.h" ]
  public_deps = [
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/fit",
  ]
}

executable("wake_lease_integration_test_cpp") {
  testonly = true
  sources = [ "wake_lease_integration_test.cc" ]
  deps = [
    ":wake_lease_cpp",
    "testing",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

executable("wake_lease_unittest_cpp") {
  testonly = true
  sources = [ "wake_lease_unittest.cc" ]
  deps = [
    ":wake_lease_cpp",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

fuchsia_test_component("wake-lease-integration-test-cpp") {
  manifest = "meta/wake_lease_integration_test_cpp.cml"
  deps = [ ":wake_lease_integration_test_cpp" ]
}

fuchsia_unittest_component("wake-lease-unittest-cpp") {
  deps = [ ":wake_lease_unittest_cpp" ]
}

fuchsia_test_package("tests") {
  package_name = "power-example-tests-cpp"
  test_components = [
    ":unmanaged-element-integration-test-cpp",
    ":unmanaged-element-unittest-cpp",
    ":wake-lease-integration-test-cpp",
    ":wake-lease-unittest-cpp",
  ]
  deps = [
    # Other components that we are injecting into integration tests.
    "//src/power/broker:component",
    "//src/power/system-activity-governor:component",
  ]
}
