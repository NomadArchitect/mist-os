# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("cpp") {
  deps = [ ":unmanaged_element_cpp" ]
}

static_library("unmanaged_element_cpp") {
  sources = [ "unmanaged_element.cc" ]
  public = [ "unmanaged_element.h" ]
  public_deps = [
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/lib/driver/power/cpp",
    "//sdk/lib/fit",
  ]
  deps = [ "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_cpp" ]
}

executable("unmanaged_element_cpp_unittest") {
  testonly = true
  sources = [ "unmanaged_element_unittest.cc" ]
  deps = [
    ":unmanaged_element_cpp",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/power/cpp/testing",
    "//sdk/lib/fit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

fuchsia_unittest_component("unmanaged-element-cpp-unittest") {
  deps = [ ":unmanaged_element_cpp_unittest" ]
}

executable("unmanaged_element_cpp_integration_test") {
  testonly = true
  sources = [ "unmanaged_element_integration_test.cc" ]
  deps = [
    ":unmanaged_element_cpp",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_cpp",
    "//sdk/lib/diagnostics/reader/cpp",
    "//sdk/lib/driver/power/cpp/testing",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
  ]
}

fuchsia_test_component("unmanaged-element-cpp-integration-test") {
  manifest = "meta/unmanaged_element_cpp_integration_test.cml"
  deps = [ ":unmanaged_element_cpp_integration_test" ]
}

fuchsia_test_package("tests") {
  package_name = "unmanaged-element-example-cpp-tests"
  test_components = [
    ":unmanaged-element-cpp-integration-test",
    ":unmanaged-element-cpp-unittest",
  ]
  deps = [
    # Other components that we are injecting into integration tests.
    "//src/power/broker:component",
  ]
  subpackages = [
    "//src/power/system-activity-governor:package",
    "//src/power/system-activity-governor:config-no-suspender",
  ]
}
