# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("rust") {
  deps = [ ":wake_lease_rust" ]
}

rustc_library("wake_lease_rust") {
  with_unit_tests = true  # Generates wake_lease_rust_test.
  name = "wake_lease"
  edition = "2021"
  source_root = "src/wake_lease.rs"
  sources = [ "src/wake_lease.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
    "//third_party/rust_crates:anyhow",
  ]
  test_deps = [
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
  ]
}

rustc_test("wake_lease_integration_test_rust") {
  edition = "2021"
  source_root = "src/wake_lease_integration_test.rs"
  sources = [ "src/wake_lease_integration_test.rs" ]
  deps = [
    ":wake_lease_rust",
    "//sdk/fidl/fuchsia.power.broker:fuchsia.power.broker_rust",
    "//sdk/fidl/fuchsia.power.system:fuchsia.power.system_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/power/broker/client",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]
}

fuchsia_test_component("wake-lease-integration-test-rust") {
  manifest = "meta/wake_lease_integration_test_rust.cml"
  deps = [ ":wake_lease_integration_test_rust" ]
}

fuchsia_unittest_component("wake-lease-unittest-rust") {
  deps = [ ":wake_lease_rust_test" ]
}

fuchsia_test_package("tests") {
  package_name = "power-example-tests-rust"
  test_components = [
    ":wake-lease-integration-test-rust",
    ":wake-lease-unittest-rust",
  ]
  deps = [
    # Other components that we are injecting into integration tests.
    "//src/power/broker:component",
    "//src/power/system-activity-governor:component",
  ]
}
