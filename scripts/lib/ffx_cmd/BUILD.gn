# Copyright 2024 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/python/python_host_test.gni")
import("//build/python/python_library.gni")
import("//build/python/python_mobly_test.gni")

group("tests") {
  testonly = true

  deps = [ ":ffx_cmd_tests($host_toolchain)" ]
}

group("e2e_tests") {
  testonly = true
  deps = [ ":ffx_cmd_inspect_e2e_test($host_toolchain)" ]
}

if (is_host) {
  python_library("ffx_cmd") {
    sources = [
      "__init__.py",
      "inspect.py",
      "lib.py",
      "version.py",
    ]
    deps = [
      "//scripts/lib/async_utils",
      "//scripts/lib/fx_cmd",
      "//src/lib/diagnostics/python/fuchsia_inspect",
    ]
  }

  python_host_test("ffx_cmd_tests") {
    main_source = "tests/test_ffx_cmd.py"
    main_callable = "unittest.main"
    extra_args = [ "-v" ]
    libraries = [
      ":ffx_cmd",
      "//scripts/lib/async_utils",
    ]
    test_data_deps = [ "//src/developer/ffx:suite_test_data" ]
  }

  python_mobly_test("ffx_cmd_inspect_e2e_test") {
    main_source = "e2e_tests/test_ffx_inspect.py"
    libraries = [
      "//src/testing/end_to_end/honeydew",
      "//src/testing/end_to_end/mobly_base_tests:fuchsia_base_test",
      "//src/lib/diagnostics/python/fuchsia_inspect",
      ":ffx_cmd",
    ]
  }
}
